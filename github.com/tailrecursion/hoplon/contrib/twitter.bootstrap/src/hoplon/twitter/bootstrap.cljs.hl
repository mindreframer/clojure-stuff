(ns hoplon.twitter.bootstrap)

(def ^:private cols-prefixes
  (for [i [:xs :sm :md :lg] j (range 1 13)]
    [(str "col-" (name i)) j]))

(defn- mkreset [sep]
  (->> cols-prefixes
    (map (fn [[k v]] [(keyword (str k sep v)) false]))
    (into {} )))

(def ^:private cols-seps
  {:col "-" :push "-push-" :pull "-pull-" :offset "-offset-"})

(def ^:private cols-reset
  (->> cols-seps (map #(vector (key %) (mkreset (val %)))) (into {})))

(defn- mkcol-key [key m]
  (reduce-kv #(assoc %1 (keyword (str "col-" (name %2) (cols-seps key) %3)) true) {} m))

(defn- do-classes [elem key val]
  (do! elem :class (merge (cols-reset key) (mkcol-key key val))))

(defmethod do! :col    [elem key val] (do-classes elem key val))
(defmethod do! :offset [elem key val] (do-classes elem key val))
(defmethod do! :push   [elem key val] (do-classes elem key val))
(defmethod do! :pull   [elem key val] (do-classes elem key val))

(defmethod do! :success [elem _ val] (do! elem :class {:has-success (boolean val)}))
(defmethod do! :warning [elem _ val] (do! elem :class {:has-warning (boolean val)}))
(defmethod do! :error   [elem _ val] (do! elem :class {:has-error   (boolean val)}))

(defelem container       [attr kids] ((div :class "container") attr kids))
(defelem form-horizontal [attr kids] ((form :role "form" :class "form-horizontal") attr kids))
(defelem control-label   [attr kids] ((label :class "control-label") attr kids))
(defelem form-group      [attr kids] ((div :class "form-group") attr kids))
(defelem checkbox        [attr kids] ((div :class "checkbox") attr kids))

(defelem active
  [{:keys [state]} [kid]]
  (kid :class (cell= {:active state})))

(defelem deck
  [{:keys [state]} kids]
  (->> kids
    (map-indexed #(active :state (cell= (= %1 state)) %2))))

(defelem selector
  [{:keys [state event]} kids]
  (->> kids
    (deck :state state)
    (map-indexed #(%2 event (fn [_] (reset! state %1))))))

(defelem tab-tab [attr [txt]] (a :href "javascript:void(0)" txt))
(defelem content [attr kids]  (div kids))
(defelem tab     [attr kids]  [(tab-tab (:name attr)) (content kids)])

(def ^:private trans (partial apply map vector))

(defelem tabs
  [{:keys [state]} kids]
  (let [[trigs ctnrs] (trans (partition 2 kids))
        state (or state (cell 0))
        trigs (selector :state state :event :click (map li trigs))
        ctnrs (deck :state state (map #(% :class "tab-pane") ctnrs))]
    [(ul :class "nav nav-tabs" trigs)
     (div :class "tab-content" ctnrs)]))
